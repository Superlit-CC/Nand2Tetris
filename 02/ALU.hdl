// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // zx
    Mux16(a=x, b=false, sel=zx, out=xo1);
    // nx
    Not16(in=xo1, out=nxo1);
    Mux16(a=xo1, b=nxo1, sel=nx, out=xo2);
    // zy
    Mux16(a=y, b=false, sel=zy, out=yo1);
    // ny
    Not16(in=yo1, out=nyo1);
    Mux16(a=yo1, b=nyo1, sel=ny, out=yo2);
    // f
    Add16(a=xo2, b=yo2, out=addXY);
    And16(a=xo2, b=yo2, out=andXY);
    Mux16(a=andXY, b=addXY, sel=f, out=o3);
    // no
    Not16(in=o3, out=no3);
    Mux16(a=o3, b=no3, sel=no, out=out, out=copy, out[15]=flagng);
    // zr ng
    And16(a=copy, b=true, out[0..7]=tmp1, out[8..15]=tmp2);
    Or8Way(in=tmp1, out=to1);
    Or8Way(in=tmp2, out=to2);
    Or(a=to1, b=to2, out=flagzr);

    Mux(a=true, b=false, sel=flagzr, out=zr);
    Mux(a=false, b=true, sel=flagng, out=ng);
}